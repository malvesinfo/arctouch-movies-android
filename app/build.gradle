apply plugin: 'com.android.application'
apply plugin: 'realm-android'

android {

    def STRING = "String"
    def BASE_URL = "BASE_URL"
    def BASE_IMAGE_URL = "BASE_IMAGE_URL"
    def API_KEY = "API_KEY"
    def BASE_APP_SITE = "BASE_APP_SITE"

    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "com.arctouch.movies"
        minSdkVersion 17
        targetSdkVersion 25
        versionCode 1
        versionName "0.1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        project.ext.set("archivesBaseName", "arctouch-movies-" + defaultConfig.versionName);

        buildConfigField STRING, BASE_URL, '"https://api.themoviedb.org/3/"'
        buildConfigField STRING, API_KEY, '"1f54bd990f1cdfb230adb312546d765d"'
        buildConfigField STRING, BASE_IMAGE_URL, '"https://image.tmdb.org/t/p"'
        buildConfigField STRING, BASE_APP_SITE, '"http://imdb.com/rg/an_share/title/title/"'
    }

    compileOptions {
        //noinspection GroovyAssignabilityCheck
        sourceCompatibility JavaVersion.VERSION_1_8
        //noinspection GroovyAssignabilityCheck
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
    }

    aaptOptions {
        cruncherEnabled = false
    }

    sourceSets {
        //noinspection GroovyAssignabilityCheck
        main {
            res.srcDirs = [
                    'src/main/res/common',
                    'src/main/res/home',
                    'src/main/res/movie',
                    'src/main/res/splash'
            ]
        }
    }

    signingConfigs {
        release {
            storeFile file(STORE_FILE_RELEASE)
            storePassword STORE_PASSWORD_RELEASE
            keyAlias KEY_ALIAS_RELEASE
            keyPassword KEY_PASSWORD_RELEASE
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    testCompile 'junit:junit:4.12'

    // If you want to bind to Android-specific lifecycles
    compile 'com.trello.rxlifecycle2:rxlifecycle-android:2.1.0'
    // If you want pre-written Activities and Fragments you can subclass as providers
    compile 'com.trello.rxlifecycle2:rxlifecycle-components:2.1.0'
    // com.trello:rx* use this dependency, avoid conflicts
    androidTestCompile 'com.google.code.findbugs:jsr305:3.0.1'

    def retrofitVersion = "2.2.0"
    compile "com.squareup.retrofit2:retrofit:${retrofitVersion}"
    compile "com.squareup.retrofit2:converter-gson:${retrofitVersion}"
    compile "com.squareup.retrofit2:adapter-rxjava2:${retrofitVersion}"
    compile 'com.squareup.okhttp3:logging-interceptor:3.3.0'

    // Support Libraries
    def appCompatVersion = "25.3.1"
    compile "com.android.support:appcompat-v7:${appCompatVersion}"
    compile "com.android.support:recyclerview-v7:${appCompatVersion}"
    compile "com.android.support:cardview-v7:${appCompatVersion}"
    compile "com.android.support:design:${appCompatVersion}"

    compile 'com.github.bumptech.glide:glide:3.7.0'

    compile 'com.lapism:searchview:4.0'

}
